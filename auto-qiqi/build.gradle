plugins {
    id 'fabric-loom' version '1.9-SNAPSHOT'
    id 'java'
}

version = "1.0.0"
group = "com.cobblemoon"

base {
    archivesName = "auto-qiqi"
}

repositories {
    mavenCentral()
    maven { url = "https://maven.impactdev.net/repository/development/" }
}

dependencies {
    minecraft "com.mojang:minecraft:1.21.1"
    mappings "net.fabricmc:yarn:1.21.1+build.3:v2"
    modImplementation "net.fabricmc:fabric-loader:0.16.10"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.116.7+1.21.1"

    modCompileOnly "com.cobblemon:fabric:1.7.0+1.21.1-SNAPSHOT"

    testImplementation "org.junit.jupiter:junit-jupiter:5.11.4"
    testImplementation "org.mockito:mockito-core:5.14.2"
    testImplementation "org.mockito:mockito-junit-jupiter:5.14.2"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

test {
    useJUnitPlatform()
}

processResources {
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
    options.encoding = "UTF-8"
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${base.archivesName.get()}" }
    }
}

tasks.register('deploy', Copy) {
    dependsOn remapJar
    from remapJar.archiveFile
    into file("${System.getProperty('user.home')}/Library/Application Support/ModrinthApp/profiles/Cobblemoon1.1.8/mods")
    doFirst {
        delete fileTree("${System.getProperty('user.home')}/Library/Application Support/ModrinthApp/profiles/Cobblemoon1.1.8/mods") {
            include "auto-qiqi-*.jar"
        }
    }
    doLast {
        println ""
        println "============================================"
        println "  auto-qiqi installed successfully!"
        println "  ${remapJar.archiveFile.get().asFile.name}"
        println "============================================"
        println ""
    }
}

build.finalizedBy deploy
